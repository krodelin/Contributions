| package |
package := Package name: 'US Collection Extensions'.
package paxVersion: 1;
	basicComment: ''.


package methodNames
	add: #Collection -> #any;
	add: #Collection -> #ifEmpty:;
	add: #Collection -> #ifEmpty:ifNotEmpty:;
	add: #Collection -> #ifNotEmpty:;
	add: #Collection -> #ifNotEmpty:ifEmpty:;
	add: #Collection -> #ifNotEmptyDo:;
	add: #Collection -> #isEmptyOrNil;
	add: #Collection -> #notNilOrEmpty;
	add: #Dictionary -> #keysAndValuesInverted;
	add: #Dictionary -> #keysAt:prefix:;
	add: #Random -> #choice:;
	add: #SequenceableCollection -> #keysAndValuesInverted;
	add: #SequenceableCollection -> #pairsDo:;
	add: #String -> #findDelimiters:startingAt:;
	add: #String -> #findTokens:;
	add: #String -> #includesSubString:;
	add: #String -> #matchesRegex:;
	add: #String -> #skipDelimiters:startingAt:;
	add: #UndefinedObject -> #isEmptyOrNil;
	add: #UndefinedObject -> #notNilOrEmpty;
	yourself.

package binaryGlobalNames: (Set new
	yourself).

package globalAliases: (Set new
	yourself).

package setPrerequisites: (IdentitySet new
	add: '..\..\Core\Object Arts\Dolphin\Base\Dolphin';
	add: '..\..\Core\Object Arts\Dolphin\System\Random\Dolphin Random Stream';
	add: '..\..\Core\Object Arts\Dolphin\ActiveX\Components\VBScript\VBScript Regular Expressions';
	yourself).

package!

"Class Definitions"!


"Global Aliases"!


"Loose Methods"!

!Collection methodsFor!

any
^self detect: [:each | true]!

ifEmpty: aBlock 
	^self isEmpty ifTrue: [aBlock value]!

ifEmpty: emptyBlock ifNotEmpty: notEmptyBlock 
	^self isEmpty ifTrue: [emptyBlock value] ifFalse: [notEmptyBlock value]!

ifNotEmpty: aBlock 
	^self isEmpty ifFalse: [aBlock value]!

ifNotEmpty: notEmptyBlock ifEmpty: emptyBlock 
	^self ifEmpty: emptyBlock ifNotEmpty: notEmptyBlock!

ifNotEmptyDo: operation 
	^self isEmpty 
		ifTrue: [nil]
		ifFalse: 
			[self do: operation.
			]!

isEmptyOrNil
	^self isEmpty!

notNilOrEmpty
	^self notEmpty! !
!Collection categoriesFor: #any!accessing!public!testing! !
!Collection categoriesFor: #ifEmpty:!operations!public! !
!Collection categoriesFor: #ifEmpty:ifNotEmpty:!operations!public! !
!Collection categoriesFor: #ifNotEmpty:!operations!public! !
!Collection categoriesFor: #ifNotEmpty:ifEmpty:!operations!public! !
!Collection categoriesFor: #ifNotEmptyDo:!operations!public! !
!Collection categoriesFor: #isEmptyOrNil!public!testing! !
!Collection categoriesFor: #notNilOrEmpty!public!testing! !

!Dictionary methodsFor!

keysAndValuesInverted
	| dic |
	dic := Dictionary new: self size.
	self keysAndValuesDo: [:eachKey :eachValue | dic at: eachValue put: eachKey].
	^dic!

keysAt: aValue prefix: aString 
	| matchingKeys |
	matchingKeys := OrderedCollection new.
	self keysAndValuesDo: 
			[:eachKey :eachValue | 
			(eachValue = aValue and: [eachKey beginsWith: aString]) ifTrue: [matchingKeys add: eachKey]].
	^matchingKeys! !
!Dictionary categoriesFor: #keysAndValuesInverted!accessing!public! !
!Dictionary categoriesFor: #keysAt:prefix:!accessing!public! !

!Random methodsFor!

choice: aCollection
^aCollection at: (self next * aCollection size + 0.5) rounde! !
!Random categoriesFor: #choice:!accessing!public! !

!SequenceableCollection methodsFor!

keysAndValuesInverted
	| dic |
	dic := Dictionary new: self size.
	self keysAndValuesDo: [:eachKey :eachValue | dic at: eachValue  put: eachKey ].
	^dic!

pairsDo: aBlock 
	"Evaluate aBlock with my elements taken two at a time.  If there's an odd number of items, ignore the last one.  Allows use of a flattened array for things that naturally group into pairs.  See also pairsCollect:"

	1 to: self size // 2 do:
		[:index | aBlock value: (self at: 2 * index - 1) value: (self at: 2 * index)]
"
#(1 'fred' 2 'charlie' 3 'elmer') pairsDo:
	[:a :b | Transcript cr; show: b, ' is number ', a printString]
"! !
!SequenceableCollection categoriesFor: #keysAndValuesInverted!accessing!public! !
!SequenceableCollection categoriesFor: #pairsDo:!accessing!public! !

!String methodsFor!

findDelimiters: delimiters startingAt: start 
	"Answer the index of the character within the receiver, starting at start, that matches one of the delimiters. If the receiver does not contain any of the delimiters, answer size + 1."

	start to: self size do: [:i |
		delimiters do: [:delim | delim = (self at: i) ifTrue: [^ i]]].
	^ self size + 1!

findTokens: delimiters
	"Answer the collection of tokens that result from parsing self.  Return strings between the delimiters.  Any character in the Collection delimiters marks a border.  Several delimiters in a row are considered as just one separation.  Also, allow delimiters to be a single character."

	| tokens keyStart keyStop separators |

	tokens := OrderedCollection new.
	separators := delimiters isCharacter 
		ifTrue: [Array with: delimiters]
		ifFalse: [delimiters].
	keyStop := 1.
	[keyStop <= self size] whileTrue:
		[keyStart := self skipDelimiters: separators startingAt: keyStop.
		keyStop := self findDelimiters: separators startingAt: keyStart.
		keyStart < keyStop
			ifTrue: [tokens add: (self copyFrom: keyStart to: (keyStop - 1))]].
	^tokens!

includesSubString: aString
^(self findString: aString)> 0!

matchesRegex: aString 
	| regex |
	regex := IRegExp2 new.
	regex pattern: aString.
	regex global: true.
	^regex test: self!

skipDelimiters: delimiters startingAt: start 
	"Answer the index of the character within the receiver, starting at start, that does NOT match one of the delimiters. If the receiver does not contain any of the delimiters, answer size + 1.  Assumes the delimiters to be a non-empty string."

	start to: self size do: [:i |
		delimiters detect: [:delim | delim = (self at: i)]
				ifNone: [^ i]].
	^ self size + 1! !
!String categoriesFor: #findDelimiters:startingAt:!public!searching! !
!String categoriesFor: #findTokens:!public!searching! !
!String categoriesFor: #includesSubString:!public!searching! !
!String categoriesFor: #matchesRegex:!public!searching! !
!String categoriesFor: #skipDelimiters:startingAt:!public!searching! !

!UndefinedObject methodsFor!

isEmptyOrNil
	^true!

notNilOrEmpty
	^false! !
!UndefinedObject categoriesFor: #isEmptyOrNil!public!testing! !
!UndefinedObject categoriesFor: #notNilOrEmpty!public!testing! !

"End of package definition"!

